/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
apply plugin: 'groovy'

sourceCompatibility = 1.8

FileUtil.baseNames(sourceSets.main.allSource.files).each { name ->
    tasks.register("run$name", JavaExec) {
        dependsOn compileGroovy
        group 'Application'
        description "Run ${name}.groovy as a JVM application/Groovy script"
        classpath = sourceSets.main.runtimeClasspath
        mainClass = name
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.apache.groovy:groovy:$groovy4Version"
    implementation 'org.codehaus.gpars:gpars:1.2.1'
    implementation "com.github.haifengl:smile-core:$smileVersion"
    implementation 'org.apache.commons:commons-math3:3.6.1'
    runtimeOnly "org.slf4j:slf4j-jdk14:$slf4jVersion"
    runtimeOnly "org.bytedeco:openblas-platform:$openblasPlatformVersion"
}
