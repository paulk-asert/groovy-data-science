/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
apply plugin: 'groovy'

repositories {
//    mavenLocal() // until next wayang release of ml4all, build latest version and install locally
    mavenCentral()
//    maven {
//        url 'https://repository.apache.org/content/repositories/orgapachewayang-1025'
//    }
}

ext {
    wayangVersion = '1.1.0'
    hadoopVersion = '3.4.1'
    scalaMajorVersion = '2.12'
    sparkVersion = '3.5.6'
}

dependencies {
    implementation "org.apache.groovy:groovy:$groovy5Version"
    implementation "org.apache.wayang:wayang-api-scala-java:$wayangVersion"
    implementation "org.apache.wayang:wayang-java:$wayangVersion"
    implementation("org.apache.wayang:wayang-ml4all:$wayangVersion") {
        exclude(group: 'org.apache.spark', module: 'spark-graphx_2.12')
        exclude(group: 'org.apache.spark', module: 'spark-mllib_2.12')
        exclude(group: 'org.apache.hadoop')
    }
    implementation("org.apache.wayang:wayang-spark:$wayangVersion") {
        transitive = false
    }
    implementation "com.fasterxml.jackson:jackson-bom:$jacksonVersion"
    runtimeOnly "org.apache.spark:spark-launcher_$scalaMajorVersion:$sparkVersion"
    runtimeOnly("org.apache.spark:spark-core_$scalaMajorVersion:$sparkVersion") {
        exclude(group: 'org.slf4j', module: '*')
        exclude(group: 'org.apache.logging.log4j', module: 'log4j-slf4j-impl')
    }
    runtimeOnly "org.slf4j:slf4j-simple:$slf4jVersion"
    runtimeOnly 'org.apache.commons:commons-configuration2:2.10.1'
    runtimeOnly 'com.fasterxml.woodstox:woodstox-core:6.6.2'
    runtimeOnly 'com.google.re2j:re2j:1.7'
    runtimeOnly("org.apache.hadoop:hadoop-common:$hadoopVersion") {
        transitive = false
    }
    runtimeOnly("org.apache.hadoop:hadoop-hdfs:$hadoopVersion") {
        transitive = false
    }
}

tasks.register('versionInfo') {
    doLast {
        File javaHome = new File(System.getProperty('java.home'))
        logger.lifecycle "Using Java from $javaHome (version ${System.getProperty('java.version')})"
        if (!JavaVersion.current().isJava8() && !JavaVersion.current().isJava11()) {
            logger.lifecycle "WARNING! Apache Wayang automates selection of the data processing platform."
            logger.lifecycle "WARNING! The current application nominates either a Java or Spark platform."
            logger.lifecycle "WARNING! The application may fail if Spark is selected and the JDK is not 8 or 11!"
        }
    }
}

def runAll = tasks.register('runAll') {
    group 'Application'
    dependsOn versionInfo
}

FileUtil.baseNames(sourceSets.main.allSource.files).each { name ->
    def subtask = tasks.register("run$name", JavaExec) {
        dependsOn compileGroovy
        group 'Application'
        description "Run ${name}.groovy as a JVM application/Groovy script"
        classpath = sourceSets.main.runtimeClasspath
        mainClass = name
        if (JavaVersion.current().isCompatibleWith(JavaVersion.VERSION_11)) {
            jvmArgs '--add-exports=java.base/sun.nio.ch=ALL-UNNAMED'
        }
    }
    runAll.configure {
        dependsOn subtask
    }
}
